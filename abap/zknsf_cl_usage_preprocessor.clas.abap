class ZKNSF_CL_USAGE_PREPROCESSOR definition
  public
  inheriting from CL_YCM_CC_USAGE_PREPROCESSOR
  create public

  global friends ZKNSF_CL_API_USAGE .

public section.

  methods IF_YCM_CC_USAGE_PREPROCESSOR~GET_OBJECT_INFOS
    redefinition .
protected section.

  types:
    BEGIN OF ty_key_user_objects,
             object_type TYPE trobjtype,
             object_name TYPE trobj_name,
           END OF ty_key_user_objects .

  data:
    key_user_objects TYPE HASHED TABLE OF ty_key_user_objects WITH UNIQUE KEY object_type object_name .

  methods PREPARE_USAGES
    redefinition .
  PRIVATE SECTION.
ENDCLASS.



CLASS ZKNSF_CL_USAGE_PREPROCESSOR IMPLEMENTATION.


  METHOD prepare_usages.
    LOOP AT usages INTO DATA(usage) WHERE object_type = 'BADI_DEF'.
      " We also want BADI_DEF in our result and the super->prepare_usages would change the object_type to ENHS
      INSERT usage INTO TABLE result.
    ENDLOOP.

    " Get rid of the BADI_DEF for the super call, to avoid duplicate ENHS entries
    DATA(standard_usages) = usages.
    DELETE standard_usages WHERE object_type = 'BADI_DEF'.

    " Get the standard prepare_usages in
    DATA(parent) = super->prepare_usages( usages = standard_usages ).

    "Merge both
    LOOP AT parent INTO DATA(line).
      INSERT line INTO TABLE result.
    ENDLOOP.
  ENDMETHOD.


  METHOD if_ycm_cc_usage_preprocessor~get_object_infos.
    CLEAR result.

    IF rfc_dest IS INITIAL. " This is not supported by the remote scenario!

      " Ignore DB Vies generated by using cds views (without "entity")
      IF zknsf_cl_key_user_util=>get_instance( )->is_cds_generated( object_type = object_type object_name = object_name ) IS NOT INITIAL.
        RETURN.
      ENDIF.

      " Ignore all Key-User Generated Objects
      IF zknsf_cl_key_user_util=>get_instance( )->is_key_user_generated( object_type = object_type object_name = object_name ) IS NOT INITIAL.
        RETURN.
      ENDIF.
    ENDIF.

    " Filter out DevClasses, as we only use them for Scoring
    IF object_type = 'DEVC'.
      RETURN.
    ENDIF.

    RETURN super->if_ycm_cc_usage_preprocessor~get_object_infos(
        usages                  = usages
        is_filtering_dlvunit    = is_filtering_dlvunit
        object_type             = object_type
        object_name             = object_name
        is_filtering_namespaces = abap_true
        allowed_namespaces      = allowed_namespaces
    ).
  ENDMETHOD.
ENDCLASS.
